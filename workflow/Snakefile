configfile: 'config.yaml'

features = config["features"]

rule all:
    input:
        evol_feat_orthogroups = 'results/evolutionary_features_by_orthogroups.tsv',
        evol_feat_genes = 'results/evolutionary_features_by_genes.tsv'

rule create_orthology_dictionaries:
    input:
        config["input_files"]["orthology_table"]
    output:
        orthogroups = 'results/orthogroups.pickle',
        genes = 'results/genes.pickle'
    script:
        'create_orthology_dictionaries.py'

rule compute_universality:
    input:
        rules.create_orthology_dictionaries.output.orthogroups
    output:
        UNI_orthogroups = 'results/evol_feat_orthogroups/UNI_orthogroups.tsv',
        UNI_genes = 'results/evol_feat_genes/UNI_genes.tsv'
    script:
        'compute_universality.py'

rule compute_duplicability:
    input:
        rules.create_orthology_dictionaries.output.orthogroups
    output:
        DUP_orthogroups = 'results/evol_feat_orthogroups/DUP_orthogroups.tsv',
        DUP_genes = 'results/evol_feat_genes/DUP_genes.tsv'
    script:
        'compute_duplicability.py'

rule compute_average_copy_number:
    input:
        rules.create_orthology_dictionaries.output.orthogroups
    output:
        ACN_orthogroups = 'results/evol_feat_orthogroups/ACN_orthogroups.tsv',
        ACN_genes = 'results/evol_feat_genes/ACN_genes.tsv'
    script:
        'compute_average_copy_number.py'

rule compute_copy_number_variation:
    input:
        rules.create_orthology_dictionaries.output.orthogroups
    output:
        CNV_orthogroups = 'results/evol_feat_orthogroups/CNV_orthogroups.tsv',
        CNV_genes = 'results/evol_feat_genes/CNV_genes.tsv'
    script:
        'compute_copy_number_variation.py'

rule merge_features:
    input:
        evol_feat_orthogroups = expand('results/evol_feat_orthogroups/{feature}_orthogroups.tsv', feature=features),
        evol_feat_genes = expand('results/evol_feat_genes/{feature}_genes.tsv', feature=features)
    output:
        evol_feat_orthogroups = 'results/evolutionary_features_by_orthogroups.tsv',
        evol_feat_genes = 'results/evolutionary_features_by_genes.tsv'
    script:
        'merge_features.py'

# rule compute_SYN:
#     input:
#         rules.create_orthology_dictionaries.output['orthogroups']
#         'data/{species}.gff'
#     output:
#         'results/{species}.tsv'
#     script:
#         'compute_UNI.py'
